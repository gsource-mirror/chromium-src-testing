# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rust_macro.gni")
import("//build/rust/rust_static_library.gni")
import("//testing/test.gni")

# TODO(crbug.com/1466674): Fuchsia and Apple-ASAN have problems that need to be
# fixed.
# Fuchsia: Missing support for the OS, we have a CL submitted upstream.
#   https://github.com/dtolnay/linkme/issues/68
# Apple-ASAN: Panics on startup, we don't know why yet.
#   https://github.com/dtolnay/linkme/issues/28#issuecomment-1654410071
# Windows: Can not link with lld-link, also crashes lld-link. Can repro on Linux
#   with cross-compilation too.
#   https://github.com/dtolnay/linkme/issues/70
rust_gtests_work = !is_win && !is_fuchsia && !(is_apple && is_asan)

if (rust_gtests_work) {
  group("rust_gtest_interop") {
    testonly = true
    public_deps = [
      ":rust_gtest_interop_cpp",
      ":rust_gtest_interop_rust",
    ]
  }

  source_set("rust_gtest_interop_cpp") {
    testonly = true
    visibility = [ ":rust_gtest_interop" ]
    sources = [
      "rust_gtest_interop.cc",
      "rust_gtest_interop.h",
    ]
    deps = [
      "//base",
      "//testing/gtest",
    ]
    defines = [ "RUST_GTEST_INTEROP_IMPLEMENTATION" ]
  }

  rust_static_library("rust_gtest_interop_rust") {
    testonly = true
    visibility = [ ":rust_gtest_interop" ]
    crate_name = "rust_gtest_interop"
    crate_root = "rust_gtest_interop.rs"
    allow_unsafe = true
    sources = [
      "expect_macros.rs",
      "rust_gtest_interop.rs",
    ]
    deps = [
      # Macros re-exported from the rust_gtest_interop crate. Can only be
      # accessed through the crate, so not public_deps.
      ":gtest_attribute",
    ]
    public_deps = [
      # This is re-exported for the gtest_attribute macros.
      "//third_party/rust/linkme/v0_3:test_support",
    ]
  }

  rust_macro("gtest_attribute") {
    testonly = true

    crate_root = "gtest_attribute.rs"
    sources = [ "gtest_attribute.rs" ]
    deps = [
      "//third_party/rust/proc_macro2/v1:lib",
      "//third_party/rust/quote/v1:lib",
      "//third_party/rust/syn/v1:lib",
    ]

    # This target's contents are exposed as part of :rust_gtest_interop.
    visibility = [ ":*" ]
  }
  test("rust_gtest_interop_unittests") {
    sources = [
      "rust_gtest_interop_unittest.rs",
      "rust_gtest_interop_unittest_main.cc",
    ]
    deps = [
      "//base",
      "//base/test:test_support",
      "//build:blink_buildflags",
      "//testing/gtest",
    ]
  }
} else {
  # An empty placeholder for the platforms where things don't work yet. To be
  # removed when `rust_gtests_work` is always true.
  test("rust_gtest_interop_unittests") {
    deps = [ "//base/test:run_all_unittests" ]
  }
}
